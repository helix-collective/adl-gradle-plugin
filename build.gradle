plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.ysb33r.gradletest' version '2.0'
}
group 'au.com.helixta.adl.gradle'
version '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.github.docker-java', name: 'docker-java-core', version: '3.2.7'
    implementation group: 'com.github.docker-java', name: 'docker-java-transport-httpclient5', version: '3.2.7'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'org.assertj:assertj-core:3.18.1'
    testImplementation 'com.github.javaparser:javaparser-core:3.18.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    gradleTest 'com.google.code.gson:gson:2.8.6'
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'au.com.helixta.adl'
            implementationClass = 'au.com.helixta.adl.gradle.AdlGradlePlugin'
        }
    }
}

test {
    useJUnitPlatform()
}

gradleTest {
    versions '6.8'

    dependsOn jar

    kotlinDsl = true

    beforeTest {
        println "  ${it.name}"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.outputLevel = JavadocOutputLevel.QUIET
    options.quiet()
    options.addStringOption('Xdoclint:none', '-quiet')
}

//Workaround for https://gitlab.com/ysb33rOrg/gradleTest/-/issues/120
configurations.runtime.dependencies.addAll(configurations.runtimeClasspath.allDependencies)

// Make source files available as resources to tests
processTestResources {
    from sourceSets.main.allSource
}
