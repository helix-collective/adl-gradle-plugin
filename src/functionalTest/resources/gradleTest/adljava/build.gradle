import au.com.helixta.adl.gradle.AdlGenerateTask

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'au.com.helixta.adl'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

apply plugin: 'au.com.helixta.adl' // applying plugin

sourceSets {
    main {
        java {
            srcDir 'generated/java'
        }
    }
}

clean {
    delete "$projectDir/generated"
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'org.assertj:assertj-core:3.18.1'
    testImplementation 'com.github.javaparser:javaparser-core:3.18.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

task adlJava(type: AdlGenerateTask) {
    version = "0.14"
    source file("$projectDir/src/main/adl")
    generations {
        java {
            outputDirectory = file("$projectDir/generated/java")
            javaPackage = "adl.test"
            verbose = true
            generateTransitive = true
            generateAdlRuntime = true
            headerComment = '''\
            Full
            of
            galahs
            '''.stripIndent()
        }
    }
}

compileJava.dependsOn(adlJava);

task runGradleTest {
    dependsOn compileJava
    dependsOn test
}

// Make source files available as resources to tests
processTestResources {
    from sourceSets.main.allSource
}

//If adl.platform system property is configured, use it to configure platform of ADL tasks
def adlPlatformProperty = System.getProperty("adl.platform")
if (adlPlatformProperty) {
    tasks.withType(au.com.helixta.adl.gradle.AdlGenerateTask) {
        platform = au.com.helixta.adl.gradle.config.AdlPlatform.valueOf(adlPlatformProperty)
    }
}

